; Script generated by the Inno Script Studio Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Dump Manager"
#define MyAppVersion "1.0"
#define MyAppExeName "DumpManager.exe"
#define PathInstalador = '..\Install'
#define Versao = '1.04.25'
#define NomeInstalador = 'DumpManager'


[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId= {{974C9965-F4CF-4F8F-A4CD-79EFA6F194D8}}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
DefaultDirName={pf}\{#MyAppName}
DisableDirPage=no 
; (mantém a tela de escolha visível)
DefaultGroupName={#MyAppName}
Compression=lzma
SolidCompression=yes
SetupIconFile="Assets\icon.ico"
OutputDir={#PathInstalador}
OutputBaseFilename={#NomeInstalador}_{#Versao}
PrivilegesRequired=admin

[Languages]
Name: "portugues"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "C:\Users\qaapp\source\repos\DumpsManager\FrontPage\bin\Release\net8.0-windows\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "Assets\icon.ico"; DestDir: "{app}"; Flags: ignoreversion
Source: "bin\Release\net8.0\windowsdesktop-runtime-8.0.15-win-x64.exe"; DestDir: "{tmp}"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; IconFilename: "{app}\icon.ico"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon


[Run]
Filename: "{tmp}\windowsdesktop-runtime-8.0.15-win-x64.exe"; \
Parameters: "/install /quiet /norestart"; \
StatusMsg: "Instalando .NET Runtime..."; \
Check: NeedsDotNet

Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

[Code]
function NeedsDotNet: Boolean;
var
  Versions: TArrayOfString;
  i: Integer;
begin
  Result := True;
  if RegGetSubkeyNames(HKLM, 'SOFTWARE\dotnet\Setup\InstalledVersions\x64\sharedfx\WindowsDesktop', Versions) then
  begin
    for i := 0 to GetArrayLength(Versions)-1 do
    begin
      if Pos('8.0', Versions[i]) = 1 then
      begin
        Result := False;
        Break;
      end;
    end;
  end;
end;